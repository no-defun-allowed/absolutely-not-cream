def forward_flick(expr p) =
  draw p..(p + (tick_x, tick_y));
enddef;
def backward_flick(expr p) =
  draw p..(p - (tick_x, tick_y));
enddef;

def c_shape =
  z4=(4.5u, x_height*3/4);
  z5=(3u, x_height);
  z6=(2u, 0);
  z7=(4.5u, x_height*c_curl_height);
  draw z4..z5..z6..z7;
enddef;

def n_shape(expr x) =
  draw (x+0.5u, x_height/2){up}..(x+2.5u, x_height)
       ..(x+4.5u, x_height/2){down}..(x+4.5u, 0){down};
enddef;

def u_shape =
  draw (0.5u, x_height){down}..(0.5u, x_height/2)..(1.5u, 0)..(4.5u, x_height){up};
enddef;

def v_shape(expr x) =
  draw (x, 0u){right}..(x+3.5u, x_height/2){up}..(x+3u, x_height);
enddef;

def backward_c_shape =
  z4=(0.5u, x_height/2);
  z5=(3.5u, x_height-0.5u);
  z6=(3.5u, 1u);
  z7=(0.5u, 0);
  draw z4{up}..z5..z6..z7{left};
enddef;

def hook(expr x) =
  draw (x, x_height){down}..(x, 0u){down}..(x-3u, -3u);
enddef;

def right_line(expr x, h, top) =
  z1=(x,top);
  z2=(x,top-h);
  draw z1..z2; forward_flick(z2);
enddef;

def left_line(expr h, top) =
  z1=(0.5u,top);
  z2=(0.5u,top-h);
  draw z1..z2; backward_flick(z1);
enddef;

beginchar(97, 6u#, x_height#, 0u#); "Lowercase a";  
  slantpen;
  c_shape;
  right_line(4.5u, x_height, x_height);
endchar;

beginchar(98, 5.5u#, 8u#, 0u#); "Lowercase b";  
  slantpen;
  backward_c_shape;
  left_line(8u, 8u);
endchar;

beginchar(99, 6u#, x_height#, 0u#); "Lowercase c";  
  slantpen;
  c_shape;
endchar;

beginchar(100, 6u#, 8u#, 0u#); "Lowercase d";  
  slantpen;
  c_shape;
  right_line(4.5u, 8u, 8u);
endchar;

beginchar(101, 5.5u#, x_height#, 0u#); "Lowercase e";  
  slantpen;
  c_shape;
  z3=(1u, 1u);
  draw z3..z4;
endchar;

beginchar(102, 3u#, 8u#, 3u#); "Lowercase f";
  slantpen;
  draw (1u, 0)..(1u, x_height){up}..(2.5u, 8u){right}..(3.5u, 8u);
  draw (0, x_height)..(2u, x_height);
endchar;

beginchar(103, 6u#, 8u#, 3u#); "Lowercase g";
  slantpen;
  c_shape;
  hook(4.5u);
endchar;

beginchar(104, 6u#, 8u#, 0); "Lowercase h";
  slantpen;
  left_line(8u, 8u);
  n_shape(0);
endchar;

beginchar(105, 2.5u#, 7u#, 0u#); "Lowercase i";
  slantpen;
  draw (0.5u, 7u);
  right_line(0.5u, x_height, x_height);
endchar;

beginchar(106, 1.5u#, 7u#, 3u#); "Lowercase j";
  slantpen;
  draw (0.5u, 7u);
  hook(0.5u);
endchar;

beginchar(107, 5u#, 8u#, 0); "Lowercase k";
  slantpen;
  left_line(8u, 8u);
  draw (0.5u, x_height/2)..(3.5u, x_height/2)..(3.5u, x_height)..(0.5u, x_height*3/4);
  draw (0.5u, x_height/2)..(3.5u, 0);
endchar;

beginchar(108, 2.5u#, 8u#, 0); "Lowercase l";
  slantpen;
  right_line(0.5u, 8u, 8u);
endchar;

beginchar(109, 10u#, x_height#, 0); "Lowercase m";
  slantpen;
  left_line(x_height, x_height);
  n_shape(0);
  n_shape(4u);
  forward_flick((8.5u, 0));
endchar;

beginchar(110, 6u#, x_height#, 0); "Lowercase n";
  slantpen;
  left_line(x_height, x_height);
  n_shape(0);
  forward_flick((4.5u, 0));
endchar;

beginchar(111, 6u#, x_height#, 0); "Lowercase o";
  slantpen;
  draw (3u, 0)..(1u, x_height)..(3u, x_height)..(5u, x_height/2)..cycle;
endchar;

beginchar(112, 5.5u#, x_height#, 3u#); "Lowercase p";  
  slantpen;
  backward_c_shape;
  left_line(8u, x_height);
endchar;

beginchar(113, 5.5u#, x_height#, 3u#); "Lowercase q";  
  slantpen;
  c_shape;
  right_line(4.5u, 8u, x_height);
endchar;

beginchar(114, 5.5u#, x_height#, 0); "Lowercase r";
  slantpen;
  left_line(x_height, x_height);
  draw (0.5u, x_height/2){up}..(2.5u, x_height){right}..(4.5u,x_height*3/4);
endchar;

beginchar(115, 4.5u#, x_height#, 3u#); "Lowercase s";  
  slantpen;
  draw (3u, x_height-0.5u){(-0.7, 0.7)}..(0, x_height*4/5)..(0, x_height*1/2+0.5u)
       ..(3u, x_height*1/2-0.5u)..(3u, 0.5u)..(0, 0.5u);
endchar;

beginchar(116, 3u#, 6u#, 3u#); "Lowercase t";  
  slantpen;
  draw (0u, x_height)..(2u, x_height);
  right_line(1u, 6u, 6u);
endchar;

beginchar(117, 6u#, x_height#, 0); "Lowercase u";
  slantpen;
  right_line(4.5u, x_height, x_height);
  u_shape;
  forward_flick((4.5u, 0));
endchar;

beginchar(118, 5u#, x_height#, 0); "Lowercase v";
  slantpen;
  left_line(x_height, x_height);
  v_shape(0.5u);
endchar;

beginchar(119, 8.5u#, x_height#, 0); "Lowercase w";
  slantpen;
  left_line(x_height, x_height);
  draw (0.5u, 0u)..(3.5u, x_height/2);
  draw (3.5u, x_height)..(3.5u, 0u);
  v_shape(3.5u);
endchar;

beginchar(120, 5u#, x_height#, 0); "Lowercase x";
  slantpen;
  draw (0.5u, 0u)..(4.5u, x_height);
  draw (4.5u, 0u)..(0.5u, x_height);
endchar;

beginchar(121, 6u#, x_height#, 3u#); "Lowercase y";
  slantpen;
  u_shape;
  hook(4.5u);
endchar;

beginchar(122, 5.5u#, x_height#, 3u#); "Lowercase z";
  slantpen;
  draw (0.5u, x_height)..(5u, x_height);
  draw (5u, x_height)..(0.5u, 0);
  draw (0.5u, 0)..(5u, 0);
endchar;
