u#:=12/8pt#;
define_pixels(u);

font_size 8u#;
font_slant 0;
font_normal_space 4u#;
font_normal_stretch 2u#;
font_normal_shrink 1u#;
font_x_height 5u#;
font_quad 6u#;

def slantpen =
  pickup pencircle xscaled 1u yscaled 0.5u rotated 30;
enddef;

def forward_flick(expr p) =
  draw p..(p + (1u, 0.5u));
enddef;
def backward_flick(expr p) =
  draw p..(p - (1u, 0.5u));
enddef;

def c_ish_shape(expr a, b) =
  z4=(4u,5u);
  z5=(0, 3u);
  z6=(2u, 0);
  z7=(4u,2u);
  draw z4..z5..z6..z7{(a,b)};
enddef;

def c_shape =
  c_ish_shape(0.7, 0.7);
enddef;

def backward_c_shape =
  z4=(0,  3u);
  z5=(3u, 4.5u);
  z6=(3u, 1u);
  z7=(0, 0);
  draw z4{up}..z5..z6..z7{left};
enddef;

def hook =
  draw (4u, 5u){down}..(4u, 0u){down}..(0u, -3u);
enddef;

def right_line(expr x, h, top) =
  z1=(x,top);
  z2=(x,top-h);
  draw z1..z2; forward_flick(z2);
enddef;

def left_line(expr h, top) =
  z1=(0,top);
  z2=(0,top-h);
  draw z1..z2; backward_flick(z1);
enddef;

beginchar(97, 6u#, 5u#, 0u#); "Lowercase a";  
  slantpen;
  c_shape;
  right_line(4u, 5u, 5u);
  labels(range 1 thru 7);
endchar;

beginchar(98, 5u#, 8u#, 0u#); "Lowercase b";  
  slantpen;
  backward_c_shape;
  left_line(8u, 8u);
  labels(range 4 thru 7);
endchar;

beginchar(99, 5u#, 5u#, 0u#); "Lowercase c";  
  slantpen;
  c_shape;
  labels(range 4 thru 7);
endchar;

beginchar(100, 5.5u#, 8u#, 0u#); "Lowercase d";  
  slantpen;
  c_shape;
  right_line(4u, 8u, 8u);
  labels(range 1 thru 7);
endchar;

beginchar(101, 5u#, 5u#, 0u#); "Lowercase e";  
  slantpen;
  c_ish_shape(0, 1);
  z3=(0.5u, 2u);
  draw z3..z4;
  labels(range 1 thru 7);
endchar;

beginchar(103, 5u#, 8u#, 3u#); "Lowercase g";
  slantpen;
  c_shape;
  hook;
endchar;

beginchar(106, 5.5u#, 8u#, 3u#); "Lowercase j";
  slantpen;
  draw (4u, 7u);
  hook;
endchar;

beginchar(108, 2u#, 8u#, 0); "Lowercase l";
  slantpen;
  right_line(0u, 8u, 8u);
endchar;

beginchar(111, 5.5u#, 5u#, 0); "Lowercase o";
  slantpen;
  draw (2u, 0)..(0u, 2.5u)..(2u, 5u)..(4u, 2.5u)..cycle;
endchar;

beginchar(112, 5u#, 5u#, 3u#); "Lowercase p";  
  slantpen;
  backward_c_shape;
  left_line(8u, 5u);
  labels(range 1 thru 7);
endchar;

beginchar(113, 5u#, 5u#, 3u#); "Lowercase q";  
  slantpen;
  c_shape;
  right_line(4u, 8u, 5u);
  labels(range 1 thru 7);
endchar;

beginchar(115, 5u#, 5u#, 3u#); "Lowercase s";  
  slantpen;
  draw (3u, 4.5u){(-0.7, 0.7)}..(0u, 4u)..(0, 3.5u)..
       (3u, 2u)..(3u, 0.5u)..(0, 0.5u);
endchar;

end
